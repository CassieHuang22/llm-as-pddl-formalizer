(define (domain blocksworld)
  (:requirements :strips :typing)
  (:types block)
  (:predicates
    (on-table ?b - block)
    (on ?b - block ?under - block)
    (clear ?b - block)
    (holding ?b - block)
    (arm-empty)
  )
  (:action pick-up
    :parameters (?b - block)
    :precondition (and (clear ?b) (on-table ?b) (arm-empty))
    :effect (and (holding ?b)
                 (not (on-table ?b))
                 (not (arm-empty))
                 (not (clear ?b))))
  (:action put-down
    :parameters (?b - block)
    :precondition (holding ?b)
    :effect (and (on-table ?b)
                 (clear ?b)
                 (arm-empty)
                 (not (holding ?b))))
  (:action stack
    :parameters (?b - block ?under - block)
    :precondition (and (holding ?b) (clear ?under))
    :effect (and (on ?b ?under)
                 (clear ?b)
                 (arm-empty)
                 (not (holding ?b))
                 (not (clear ?under))))
  (:action unstack
    :parameters (?b - block ?under - block)
    :precondition (and (on ?b ?under) (clear ?b) (arm-empty))
    :effect (and (holding ?b)
                 (clear ?under)
                 (not (on ?b ?under))
                 (not (arm-empty))
                 (not (clear ?b))))
)