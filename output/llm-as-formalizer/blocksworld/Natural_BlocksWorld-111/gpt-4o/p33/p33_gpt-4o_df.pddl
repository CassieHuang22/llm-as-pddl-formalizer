(define (domain blocksworld)
  (:requirements :strips :typing)
  (:types block)
  (:predicates
    (on ?x - block ?y - block)
    (ontable ?x - block)
    (clear ?x - block)
    (holding ?x - block)
    (armempty)
  )
  (:action pickup
    :parameters (?x - block)
    :precondition (and (clear ?x) (ontable ?x) (armempty))
    :effect (and (holding ?x) (not (armempty)) (not (ontable ?x)) (not (clear ?x)))
  )
  (:action putdown
    :parameters (?x - block)
    :precondition (holding ?x)
    :effect (and (armempty) (ontable ?x) (clear ?x) (not (holding ?x)))
  )
  (:action stack
    :parameters (?x - block ?y - block)
    :precondition (and (holding ?x) (clear ?y))
    :effect (and (not (holding ?x)) (not (clear ?y)) (clear ?x) (armempty) (on ?x ?y))
  )
  (:action unstack
    :parameters (?x - block ?y - block)
    :precondition (and (on ?x ?y) (clear ?x) (armempty))
    :effect (and (holding ?x) (clear ?y) (not (clear ?x)) (not (armempty)) (not (on ?x ?y)))
  )
)
